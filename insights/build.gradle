apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

ext {
    GROUP = 'com.algolia.instantsearch-android'
    BASENAME = 'insights'
    CODENAME = GROUP + ":" + BASENAME
    NAME = 'InstantSearch Insights Android'
    DESC = "InstantSearch Insights Android library allows developers to report click and conversion metrics related to search queries"
    GITHUB = "algolia/instantsearch-android-insights"
    WEBSITE = "https://github.com/" + GITHUB
    REPO = WEBSITE + ".git"
    LICENSE = 'MIT'
    LICENSE_URL = "http://www.opensource.org/licenses/mit-license.php"
    VERSION = '1.0.4'
    VERSION_DESC = 'Version ' + VERSION

    VERSION_APPCOMPAT = '28.0.0'
}

group = project.ext.GROUP
version = project.ext.VERSION

android {
    compileSdkVersion sdkCompile
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion sdkMin
        targetSdkVersion sdkTarget
        versionCode versionCode
        versionName versionName
        consumerProguardFiles "proguard-rules.pro"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        all {
            buildConfigField "String", "ALGOLIA_APPLICATION_ID", "\"$System.env.ALGOLIA_APPLICATION_ID\""
            buildConfigField "String", "ALGOLIA_API_KEY", "\"$System.env.ALGOLIA_API_KEY\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                testLogging {
                    events "passed", "started", "skipped", "failed", "standardOut", "standardError"
                    exceptionFormat "full"
                    outputs.upToDateWhen { false }
                }
            }
        }
    }
}

dokka {
    reportUndocumented = false
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ['-module-name', project.ext.CODENAME]
}

task sourcesJar(type: Jar) {
    dependsOn "assembleRelease"
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier "javadoc"
    from  "$buildDir/javadoc"
}

// add sources jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
    archives file: new File("${project.buildDir}/outputs/aar/${project.name}-release.aar"),
            name: "${project.name}-release.aar",
            type: "aar"
}

dependencies {
    implementation dependency_jvm.kotlin_stdlib
    implementation dependency_android.work_manager
    implementation dependency_android.appcompat
    implementation dependency_android.espresso

    testImplementation dependency_jvm.kotlin_test
    testImplementation dependency_jvm.json
}

repositories {
    mavenCentral()
}

// Apply the bintray upload task
apply from: 'https://raw.githubusercontent.com/algolia/instantsearch-mobile-tools/03b5d3e97302021cc3516456f7955a4708ba82a0/gradle/bintrayv.gradle'